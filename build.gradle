///////////////////////////////////////////////////////////////////////////////
//
// Build Script for building JavaPOS Device Controls Library
//
// Author: denis.kuniss@dieboldnixdorf.com (2021)
//
///////////////////////////////////////////////////////////////////////////////

plugins {
	id 'java-library'
	id 'signing'
	id 'eclipse'
	id 'maven-publish'
	id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id 'jacoco'
}

ext.githubProjectUrl = 'https://github.com/JavaPOSWorkingGroup/javapos-controls'

wrapper {
	gradleVersion = '7.1.1'
}

///////////////////////////////////////////////////////////////////////////////
// Names and Versions
///////////////////////////////////////////////////////////////////////////////

def artifactName = 'javapos-controls'
group = 'org.javapos'
def uposVersion = '1.14'
version="${uposVersion}.2-SNAPSHOT" // the last part after dot is the build/release version

// dependency versions
def javaposContractsVersion = "$uposVersion.3"


///////////////////////////////////////////////////////////////////////////////
// Build Dependencies
///////////////////////////////////////////////////////////////////////////////

repositories {
	mavenCentral()

    if (!System.getenv('CI')) 
    { // evaluates only at development environment, not at CI
	    maven {
	        url "file:$localMavenRepo" // must be defined in gradle.properties
	    }
	}
}

dependencies {
	api "org.javapos:javapos-contracts:$javaposContractsVersion"
	api "org.javapos:javapos-config-loader:2.2.0"
	
	testImplementation "junit:junit:4.+"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
}

///////////////////////////////////////////////////////////////////////////////
// Project Configurations
///////////////////////////////////////////////////////////////////////////////

def javaposManifest = 	manifest {
        attributes('Specification-Title': 'UnifiedPOS Standard',
				   'Specification-Vendor': 'UnifiedPOS Committee',
                   'Specification-Version': uposVersion,
                   'Implementation-Title': 'JavaPOS Device Controls',
				   'Implementation-Vendor': 'github.com/JavaPOSWorkingGroup',
                   'Implementation-Version': version)
}

sourceCompatibility = 8

///////////////////////////////////////////////////////////////////////////////
// Build Tasks
///////////////////////////////////////////////////////////////////////////////

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
	baseName = artifactName
	manifest = javaposManifest
	from ('CHANGELOG.md') {
		into 'META-INF'
	}
}

sourcesJar {
	manifest = javaposManifest
}

///////////////////////////////////////////////////////////////////////////////
// Artifact Upload
///////////////////////////////////////////////////////////////////////////////

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
			pom {
				name = artifactName
				description = 'JavaPOS Device Controls Library'
				url = 'https://github.com/JavaPOSWorkingGroup/javapos-controls'
				licenses {
					license {
						name = 'Common Public License (CPL) -- V1.0'
						url = 'https://www.eclipse.org/legal/cpl-v10.html'
					}
				}
				developers {
					developer {
						id = 'javapos'
						name = 'JavaPOS Working Group'
						email = 'builder@javapos.org'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/JavaPOSWorkingGroup/javapos-controls.git'
					developerConnection = 'scm:git:https://github.com/JavaPOSWorkingGroup/javapos-controls.git'
					url = 'https://github.com/JavaPOSWorkingGroup/javapos-controls.git'
				}
			}
		}
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
