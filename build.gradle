///////////////////////////////////////////////////////////////////////////////
//
// Build Script for building JavaPOS Device Controls Library
//
// Author: denis.kuniss@dieboldnixdorf.com (2021)
//
///////////////////////////////////////////////////////////////////////////////

plugins {
	id 'java-library'
	id 'signing'
	id 'eclipse'
	id 'maven-publish'
	id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
	id 'project-report'
//    id 'jacoco'
}

ext.githubProjectUrl = 'https://github.com/JavaPOSWorkingGroup/javapos-controls'

wrapper {
	gradleVersion = '8.14.3'
}

///////////////////////////////////////////////////////////////////////////////
// Names and Versions
///////////////////////////////////////////////////////////////////////////////

def artifactName = 'javapos-controls'
group = 'org.javapos'
def uposVersion = '1.15' // if this version is going to be changed, first add "-SNAPSHOT" 
// to the 'version' variable below for publishing to MavenCentral's Snapshot repo first as test
version="${uposVersion}.0" // the last part after dot is the build/release version

// dependency versions
def javaposContractsVersion = "$uposVersion.0"


///////////////////////////////////////////////////////////////////////////////
// Build Dependencies
///////////////////////////////////////////////////////////////////////////////

repositories {
	mavenCentral()

    // for resolving snapshots from MavenCentral if not going for a real release
    if (!isReleaseRun()) 
    {
   		logger.warn("WARN: Seems not to be a release, so Maven Central snapshot repository is fetched!");
	    maven {
	        url 'https://oss.sonatype.org/content/repositories/snapshots'
	    }
    }

    if (!System.getenv('CI')) {
        mavenLocal()
    }
}

// this gets true if Github Actions workflow defines github.event.action project variable at Gradle command line
// AND the event action is 'released' (see release.yml)
def boolean isReleaseRun() {
	return System.getenv('CI') && null != findProperty('github.event.action') && 'released'.equals(getProperty('github.event.action')) 
}

// check whether github.event.action is defined on CI
if (System.getenv('CI') && null == findProperty('github.event.action'))
	logger.warn('WARN: Project property github.event.action is not defined, so, it cannot be determined whether a release is ongoing.')

dependencies {
	api "org.javapos:javapos-contracts:$javaposContractsVersion"
	api "org.javapos:javapos-config-loader:2.2.0"
	
	testImplementation "junit:junit:4.+"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
}

///////////////////////////////////////////////////////////////////////////////
// Project Configurations
///////////////////////////////////////////////////////////////////////////////

def javaposManifest = java.manifest {
        attributes('Specification-Title': 'UnifiedPOS Standard',
				   'Specification-Vendor': 'UnifiedPOS Committee',
                   'Specification-Version': uposVersion,
                   'Implementation-Title': 'JavaPOS Device Controls',
				   'Implementation-Vendor': 'github.com/JavaPOSWorkingGroup',
                   'Implementation-Version': version)
}

///////////////////////////////////////////////////////////////////////////////
// Build Tasks
///////////////////////////////////////////////////////////////////////////////

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
}

jar {
	archiveBaseName = artifactName
	manifest = javaposManifest
	from ('CHANGELOG.md') {
		into 'META-INF'
	}
}

sourcesJar {
	manifest = javaposManifest
    from ('CHANGELOG.md') {
        into 'META-INF'
    }
}

///////////////////////////////////////////////////////////////////////////////
// Artifact Upload
///////////////////////////////////////////////////////////////////////////////

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
			pom {
				name = artifactName
				description = 'JavaPOS Device Controls Library'
                url = githubProjectUrl
				licenses {
					license {
						name = 'Common Public License (CPL) -- V1.0'
						url = 'https://www.eclipse.org/legal/cpl-v10.html'
					}
				}
				developers {
					developer {
						id = 'javapos'
						name = 'JavaPOS Working Group'
						email = 'builder@javapos.org'
					}
				}
				scm {
                    connection = "scm:${githubProjectUrl}.git"
                    developerConnection = "scm:git:${githubProjectUrl}.git"
                    url = "${githubProjectUrl}.git"
				}
			}
		}
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
