// for enabling Gradle scan at GitHub Actions
plugins {
    id("com.gradle.develocity") version "4.3"
}

rootProject.name = 'javapos-controls'

def boolean isRunningOnBuildServer = System.getenv('CI')
def boolean isDevHost = !isRunningOnBuildServer  && !hasProperty('disableIDEDependencySubstitution')

if (startParameter.searchUpwards && isDevHost)
{
// comment this out if you want to use binary dependencies
	includeBuildProject('javapos-contracts')
	includeBuildProject('javapos-config-loader')
}

def includeBuildProject(projectName) {
	if (file("../$projectName").exists()) {
	    includeBuild ("../$projectName") {
	        dependencySubstitution {
	            substitute module("org.javapos:$projectName") using project(':')
	        }
        }
    }
}

// enabling Gradle scan by agreeing to term of service
develocity {
    buildScan {
        publishing.onlyIf { isRunningOnBuildServer }
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"
    }
}
